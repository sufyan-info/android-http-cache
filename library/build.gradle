buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def VERSION_CODE = 3
def VERSION_NAME = "1.0." + VERSION_CODE

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 30
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    google()
    jcenter()
}

def libraryGroupId = 'com.ornoma.http'
def libraryArtifactId = 'httpcache'
def libraryVersion = VERSION_NAME

dependencies {
    implementation 'org.slf4j:slf4j-android:1.7.21'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion
            }
        }
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.token')
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'http-repo'
        name = libraryArtifactId
        desc = 'Http Caching Library'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sufyan-info/android-http-cache.git'
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            released = new Date()
        }
    }
}
